defaultTasks 'clean', 'build'

apply from: '../../../cgs-common.gradle'

apply plugin: 'spring-boot'

bootRepackage {
    enabled = false
}

mainClassName = 'org.t2k.cgs.Application' // because we have other classes with main method

buildscript {

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.8.RELEASE") // fixme - version should be taken from cgs-common.gradle
    }
}

task bootRunDev(type: org.springframework.boot.gradle.run.BootRunTask, dependsOn: 'build') {
    group = 'Application'
    doFirst() {
        main = project.mainClassName
        classpath = sourceSets.main.runtimeClasspath
        systemProperty 'spring.profiles.active', 'dev'
    }
}

bootRun {
    doFirst() {
        systemProperty 'spring.profiles.active', System.properties['spring.profiles.active']
    }
}

if (hasProperty('findBugs')) {
    apply plugin: 'findbugs'

    findbugs {
        ignoreFailures = true
    }
}

dependencies {
	compile web.mvc
	compile project(':projects:backend:cgs-domain-model')
    compile project(':projects:backend:cgs-service')
    compile project(':projects:backend:cgs-shared')
    compile servlet
    compile libraries.gjson

    testCompile project(':projects:backend:cgs-backend-shared-test')
}
